_pkgname=moon-buggy
pkgname=$_pkgname-git
pkgver=c6a7f2d
pkgrel=1
pkgdesc="Moon Buggy is a simple game for the text mode"
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/seehuhn/moon-buggy.git"
license=('GPL2')
depends=('ncurses')
makedepends=('automake' 'texinfo')
options=('!emptydirs')
source=("git+https://github.com/seehuhn/moon-buggy.git"
        "Makefile.in.patch")
sha256sums=('SKIP'
            '985df74c531400d3cba8ef3a8bc3c2fcfcc53efb42f1430c04b1a971108ca73c')

prepare() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh
  patch -Np0 -i ${srcdir}/Makefile.in.patch
}

pkgver() {
  cd "$srcdir/$_pkgname"
  TAG="$(./configure --version | head -1 | grep -oE '[0-9]+.*')"
  HASH="$(git describe --always --dirty)"
  echo "$TAG_$HASH"
}

build() {
  cd "$srcdir/$_pkgname"

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/info/dir
}
